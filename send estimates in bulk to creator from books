organizationID = organization.get("organization_id");
cust_id = List({2778636000200015172});
for each  cust in cust_id
{
	// PER-CUSTOMER COUNTERS
	createdCount = 0;
	updatedCount = 0;
	// CHECK CUSTOMER ONCE IN CREATOR
	search_customer = zoho.creator.getRecords("meydanfreezone","zoho-books-customer-portal","All_Customers","Books_Customer_ID ==" + cust,1,1,"creatorapp");
	if(search_customer == null || !search_customer.containKey("data") || search_customer.get("data").isEmpty())
	{
		info "Customer not found in Creator. Skipping all estimates ----> " + cust;
		continue;
	}
	customer = search_customer.get("data").get(0).get("ID").toLong();
	channel_partner = null;
	channel_data = search_customer.get("data").get(0).get("Channel_Partner");
	if(channel_data != null && channel_data.get("ID") != null)
	{
		channel_partner = channel_data.get("ID").toLong();
	}
	info "Processing estimates for Customer ----> " + cust;
	// FETCH ESTIMATES FROM BOOKS
	list_estimates = invokeurl
	[
		url :"https://www.zohoapis.com/books/v3/estimates?organization_id=" + organizationID + "&customer_id=" + cust
		type :GET
		connection:"zohobookconnection"
	];
	if(list_estimates == null || list_estimates.get("message") != "success" || list_estimates.get("estimates") == null)
	{
		info "No estimates found for customer ----> " + cust;
		continue;
	}
	estimateList = list_estimates.get("estimates");
	info "Total estimates fetched ----> " + estimateList.size();
	for each  estimate in estimateList
	{
		estimateID = estimate.get("estimate_id");
		// FETCH ESTIMATE PDF
		file = invokeurl
		[
			url :"https://www.zohoapis.com/books/v3/estimates/" + estimateID + "?organization_id=" + organizationID + "&accept=pdf"
			type :GET
			connection:"zohobookconnection"
		];
		// FETCH ESTIMATE DETAILS
		response = invokeurl
		[
			url :"https://www.zohoapis.com/books/v3/estimates/" + estimateID + "?organization_id=" + organizationID
			type :GET
			connection:"zohobookconnection"
		];
		if(response.get("message") != "success" || response.getJSON("estimate") == null)
		{
			info "Invalid estimate response ----> " + estimateID;
			continue;
		}
		data = response.getJSON("estimate");
		estimate_id = data.get("estimate_id");
		estimate_number = data.get("estimate_number");
		estimate_date = data.get("date");
		if(estimate_date != null && estimate_date != "")
		{
			estimate_date = estimate_date.toDate();
		}
		expiry_date = data.get("expiry_date");
		if(expiry_date != null && expiry_date != "")
		{
			expiry_date = expiry_date.toDate();
		}
		status = data.get("status");
		total = data.get("total");
		salesperson = data.get("salesperson_name");
		// PREPARE ESTIMATE MAP
		dataMap = Map();
		dataMap.put("Books_Estimate_ID",estimate_id);
		dataMap.put("Books_Customer_ID",cust);
		dataMap.put("Estimate_No",estimate_number);
		dataMap.put("Estimate_Date",estimate_date);
		dataMap.put("Expiry_Date",expiry_date);
		dataMap.put("Status",status);
		dataMap.put("Total",total);
		dataMap.put("Sales_Person",salesperson);
		dataMap.put("Customer",customer);
		dataMap.put("Channel_Partner",channel_partner);
		// UPSERT ESTIMATE
		search_estimate = zoho.creator.getRecords("meydanfreezone","zoho-books-customer-portal","All_Estimates","Books_Estimate_ID == \"" + estimate_id + "\"",1,1,"creatorapp");
		recordId = null;
		if(search_estimate != null && search_estimate.containKey("data") && search_estimate.get("data").size() > 0)
		{
			recordId = search_estimate.get("data").get(0).get("ID").toLong();
			zoho.creator.updateRecord("meydanfreezone","zoho-books-customer-portal","All_Estimates",recordId,dataMap,Map(),"creatorapp");
			updatedCount = updatedCount + 1;
		}
		else
		{
			create_response = zoho.creator.createRecord("meydanfreezone","zoho-books-customer-portal","Estimates",dataMap,Map(),"creatorapp");
			if(create_response.get("code") == 3000)
			{
				recordId = create_response.get("data").get("ID").toLong();
				createdCount = createdCount + 1;
			}
		}
		// UPLOAD PDF
		if(recordId != null && isFile(file))
		{
			try 
			{
				file.setParamName("file");
				invokeurl
				[
					url :"https://www.zohoapis.com/creator/v2.1/data/meydanfreezone/zoho-books-customer-portal/report/All_Estimates/" + recordId + "/Estimate_Attachment/upload"
					type :POST
					files:file
					connection:"creatorapp"
				]
			}
			catch (e)
			{
				info "PDF upload failed for Estimate ----> " + estimate_number;
			}
		}
	}
	// PER-CUSTOMER SUMMARY LOG
	// 	info "SUMMARY for Customer " + cust;
	info "Estimates Created ----> " + createdCount;
	info "Estimates Updated ----> " + updatedCount;
	info "Total Processed ----> " + (createdCount + updatedCount);
}
